apply plugin: 'checkstyle'
apply plugin: 'findbugs'
apply plugin: 'pmd'

def configDir = "${project.rootDir}/config_quality"
def reportsDir = "${project.buildDir}/reports"

checkstyle {
    toolVersion "8.7"
}

task checkstyle(type: Checkstyle) {
    group 'verification'

    configFile = new File("$configDir/checkstyle/checkstyle.xml")
    source = fileTree('src/main/java')
    source 'src'
    include '**/*.java'
    exclude '**/gen/**'

    classpath = files()
    reports {
        xml.enabled = true
        html.enabled = true
        xml {
            destination new File("$project.buildDir/reports/checkstyle/checkstyle.xml")
        }
        html {
            destination new File("$project.buildDir/reports/checkstyle/checkstyle.html")
        }
    }
}

task findbugs(type: FindBugs, dependsOn: "assembleDebug") {
    group 'verification'

    ignoreFailures = false
    effort = "max"
    reportLevel = "high"
    excludeFilter = new File("$configDir/findbugs/findbugs-filter.xml")
    classes = files("${project.rootDir}/app/build/intermediates/classes")

    source 'src'
    include '**/*.java'
    exclude '**/gen/**'

    reports {
        xml.enabled = false
        html.enabled = true
        xml {
            destination new File("$reportsDir/findbugs/findbugs.xml")
        }
        html {
            destination new File("$reportsDir/findbugs/findbugs.html")
        }
    }

    classpath = files()
}

task pmd (type: Pmd) {

    description 'Run pmd'
    group 'verification'

    ruleSetFiles = files("${project.rootDir}/config_quality/pmd/pmd-ruleset.xml")

    source 'src'
    include '**/*.java'
    exclude '**/gen/**'

    reports {
        xml.enabled = true
        html.enabled = true
        xml {
            destination new File("$project.buildDir/reports/pmd/pmd.xml")
        }
        html {
            destination new File("$project.buildDir/reports/pmd/pmd.html")
        }
    }
}